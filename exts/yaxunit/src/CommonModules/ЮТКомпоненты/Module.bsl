//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2023 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Возвращает внешнюю компоненту, реализующую паузу
// 
// Возвращаемое значение:
//  ВнешнийОбъект
Функция Пауза() Экспорт
	
	Возврат СоздатьКомпоненту(ОписаниеКомпонентыПауза());
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Создает внешнюю компоненту по описанию
// 
// Параметры:
//  ОписаниеКомпоненты - см. ОписаниеКомпоненты
// 
// Возвращаемое значение:
//  ВнешнийОбъект
Функция СоздатьКомпоненту(ОписаниеКомпоненты) Экспорт
	
	Если ПодключитьКомпоненту(ОписаниеКомпоненты.ИмяМакета, ОписаниеКомпоненты.ИмяКомпоненты, Истина) Тогда
		Возврат Новый (ОписаниеКомпоненты.ИмяКласса);
	Иначе
		ВызватьИсключение "Не удалось подключить внешнюю компоненту " + ОписаниеКомпоненты.ИмяКласса;
	КонецЕсли;
	
КонецФункции

// Подключает внещнюю компоненту из макета
// 
// Параметры:
//  ИмяМакета - Строка - Имя макета, в котором хранится компонента
//  ИмяКомпоненты - Строка - Имя компоненты
//  Кэшировать - Булево - Кэшировать создание компоненты
// 
// Возвращаемое значение:
//  Булево - Успешность подключения
Функция ПодключитьКомпоненту(ИмяМакета, ИмяКомпоненты, Кэшировать) Экспорт
	
	Если Кэшировать Тогда
		Возврат ЮТПовторногоИспользования.ПодключитьКомпоненту(ИмяМакета, ИмяКомпоненты);
	КонецЕсли;
	
	Возврат ПодключитьВнешнююКомпоненту(ИмяМакета, ИмяКомпоненты, ТипВнешнейКомпоненты.Native);
	
КонецФункции

// Описание компоненты реализующей паузу.
// 
// Возвращаемое значение:
//  см. ОписаниеКомпоненты
Функция ОписаниеКомпонентыПауза() Экспорт
	
	Возврат ОписаниеКомпоненты("ОбщийМакет.ЮТYaxUnitAddIn", "YaxUnitAddin", "Sleep");
	
КонецФункции

// Описание внешней компоненты.
// 
// Параметры:
//  ИмяМакета - Строка - Имя макета, в котором хранится компонента
//  ИмяКомпоненты - Строка -Имя компоненты
//  ИмяКласса - Строка - Имя класса, по которому создается компонента
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Описание компоненты:
// * ИмяМакета - Строка - Имя макета, в котором хранится компонента
// * ИмяКомпоненты - Строка -Имя компоненты
// * ИмяКласса - Строка - Имя класса
Функция ОписаниеКомпоненты(ИмяМакета, ИмяКомпоненты, ИмяКласса) Экспорт
	
	Описание = Новый Структура;
	Описание.Вставить("ИмяМакета", ИмяМакета);
	Описание.Вставить("ИмяКомпоненты", ИмяКомпоненты);
	Описание.Вставить("ИмяКласса", СтрШаблон("AddIn.%1.%2", ИмяКомпоненты, ИмяКласса));
	
	Возврат Новый ФиксированнаяСтруктура(Описание);
	
КонецФункции

#КонецОбласти
