////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ОБЪЕКТА

Процедура ОбработкаПроведения(Отказ, Режим)

	// Создание движений в регистре накопления ТоварныеЗапасы
	Движения.ТоварныеЗапасы.Записывать = Истина;
	Для каждого ТекСтрокаТовары Из Товары Цикл

		Движение = Движения.ТоварныеЗапасы.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Товар = ТекСтрокаТовары.Товар;
		Движение.Склад = Склад;
		Движение.Количество = ТекСтрокаТовары.Количество;

	КонецЦикла;

	// Создание движения в регистре накопления Взаиморасчеты
	Движения.Взаиморасчеты.Записывать = Истина;
	Движение = Движения.Взаиморасчеты.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Контрагент = Поставщик;
	Движение.Валюта = Валюта;

	Если Валюта.Пустая() Тогда

		Движение.Сумма = Товары.Итог("Сумма");

	Иначе

		Курс = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Валюта)).Курс;

		Если Курс = 0 Тогда
			Движение.Сумма = Товары.Итог("Сумма");
		Иначе
			Движение.Сумма = Товары.Итог("Сумма") / Курс;
		КонецЕсли;

	КонецЕсли;
	
	СформироватьПроводкиБухгалтерия();

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") Тогда

		ЗапросПоКонтрагенту = Новый Запрос("ВЫБРАТЬ
		                                   |	Контрагенты.ЭтоГруппа
		                                   |ИЗ
		                                   |	Справочник.Контрагенты КАК Контрагенты
		                                   |ГДЕ
		                                   |	Контрагенты.Ссылка = &КонтрагентСсылка");
		ЗапросПоКонтрагенту.УстановитьПараметр("КонтрагентСсылка", ДанныеЗаполнения);
		Выборка = ЗапросПоКонтрагенту.Выполнить().Выбрать();
		Если Выборка.Следующий() И Выборка.ЭтоГруппа Тогда
			Возврат;
		КонецЕсли;
		
		Поставщик = ДанныеЗаполнения.Ссылка;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//Удалим из списка проверяемых реквизитов валюту, если по организации не ведется 
	//валютный учет
	Если НЕ ПолучитьФункциональнуюОпцию("ВалютныйУчет", Новый Структура("Организация", Организация)) Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Валюта"));
	КонецЕсли;	
	
КонецПроцедуры

Процедура СформироватьПроводкиБухгалтерия()
	
	Курс = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Валюта)).Курс;

	Если Курс = 0 Тогда
		Сумма = Товары.Итог("Сумма");
	Иначе
		Сумма = Товары.Итог("Сумма") / Курс;
	КонецЕсли;
	
	Движения.Основной.Записывать = Истина;
	Проводка = Движения.Основной.ДобавитьКредит();
	Проводка.Организация = Организация;
	Проводка.Период = Дата;
	Проводка.Сумма = Сумма;
	Проводка.Счет = ПланыСчетов.Основной.Деньги;
	Проводка.Субконто[ПланыВидовХарактеристик.ВидыСубконто.Контрагент] = Поставщик;
	Проводка.Субконто[ПланыВидовХарактеристик.ВидыСубконто.Валюта] = Валюта;
	Проводка.Субконто[ПланыВидовХарактеристик.ВидыСубконто.РасчетныйСчетКонтрагента] = РасчетныйСчетКонтрагента(Поставщик);
	
	Проводка = Движения.Основной.ДобавитьДебет();
	Проводка.Организация = Организация;
	Проводка.Период = Дата;
	Проводка.Сумма = Сумма;
	Проводка.Счет = ПланыСчетов.Основной.Товары;
	Проводка.Субконто[ПланыВидовХарактеристик.ВидыСубконто.Контрагент] = Поставщик;
	Проводка.Субконто[ПланыВидовХарактеристик.ВидыСубконто.Склад] = Склад;
	
КонецПроцедуры

Функция РасчетныйСчетКонтрагента(Контрагент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасчетныеСчетаКонтрагентов.Ссылка
	|ИЗ
	|	Справочник.РасчетныеСчетаКонтрагентов КАК РасчетныеСчетаКонтрагентов
	|ГДЕ
	|	РасчетныеСчетаКонтрагентов.Владелец = &Владелец
	|	И НЕ РасчетныеСчетаКонтрагентов.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Владелец", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции
