#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ДобавитьТест("Инициализация")
		.ДобавитьКлиентскийТест("ЗагрузитьНастройки")
		.ДобавитьТест("ИсключитьТест")
	;
	
КонецПроцедуры

Процедура Инициализация() Экспорт
	
	
	
КонецПроцедуры

Процедура ЗагрузитьНастройки() Экспорт
	
	ДымовыеТесты = Новый Структура;
	
	ПараметрыЗапуска = Новый Структура;
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ЗагрузитьНастройки(ПараметрыЗапуска))
		.Свойство("Использовать").ЭтоЛожь();
	
	ПараметрыЗапуска.Вставить("ДымовыеТесты", Неопределено);
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ЗагрузитьНастройки(ПараметрыЗапуска))
		.Свойство("Использовать").ЭтоЛожь();
	
	ПараметрыЗапуска.Вставить("ДымовыеТесты", Ложь);
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ЗагрузитьНастройки(ПараметрыЗапуска))
		.Свойство("Использовать").ЭтоЛожь();
	
	ПараметрыЗапуска.Вставить("ДымовыеТесты", Истина);
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ЗагрузитьНастройки(ПараметрыЗапуска))
		.Свойство("Использовать").ЭтоИстина();
	
	ПараметрыЗапуска.Вставить("ДымовыеТесты", ДымовыеТесты);
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ЗагрузитьНастройки(ПараметрыЗапуска))
		.Свойство("Использовать").ЭтоЛожь();
	
	ДымовыеТесты.Вставить("Использовать", Истина);
	ПараметрыЗапуска.Вставить("ДымовыеТесты", ДымовыеТесты);
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ЗагрузитьНастройки(ПараметрыЗапуска))
		.Свойство("Использовать").ЭтоИстина();
	
	ДымовыеТесты.Вставить("Справочник", Истина);
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ЗагрузитьНастройки(ПараметрыЗапуска))
		.Свойство("Подчиненные.Справочник.Использовать").ЭтоИстина();
	
	ДымовыеТесты.Вставить("Исключения", "Номенклатура");
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ЗагрузитьНастройки(ПараметрыЗапуска))
		.Свойство("Исключения[0]").Равно("Номенклатура");
	
	ДымовыеТесты.Вставить("Исключения", ЮТКоллекции.ЗначениеВМассиве("Номенклатура"));
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ЗагрузитьНастройки(ПараметрыЗапуска))
		.Свойство("Исключения[0]").Равно("Номенклатура");
	
	НастройкиСправочников = Новый Структура;
	ДымовыеТесты.Вставить("Справочник", НастройкиСправочников);
	НастройкиСправочников.Вставить("Исключения", "Удалить*");
	НастройкиСправочников.Вставить("Номенклатура", Новый Структура("ФормаСписка, Исключения", Ложь, "Скопированный"));
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ЗагрузитьНастройки(ПараметрыЗапуска))
		.Свойство("Подчиненные.Справочник.Исключения[0]").Равно("Удалить*")
		.Свойство("Подчиненные.Справочник.Подчиненные.Номенклатура.Использовать").ЭтоИстина()
		.Свойство("Подчиненные.Справочник.Подчиненные.Номенклатура.Подчиненные.ФормаСписка.Использовать").ЭтоЛожь()
		.Свойство("Подчиненные.Справочник.Подчиненные.Номенклатура.Исключения[0]").Равно("Скопированный")
	;
	
КонецПроцедуры

Процедура ИсключитьТест() Экспорт
	
	Настройки = Новый Структура("Использовать, Справочник", Истина, Ложь);
	
	УстановитьНастройки(Настройки);
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Справочник"))
		.ЭтоИстина();
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Справочник.Номенклатура"))
		.ЭтоИстина();
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Документ"))
		.ЭтоЛожь();
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Документ.ПКО"))
		.ЭтоЛожь();
	
	Настройки.Справочник = Новый Структура("Исключения", "Номенклатура");
	УстановитьНастройки(Настройки);
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Справочник"))
		.ЭтоЛожь();
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Справочник.Номенклатура"))
		.ЭтоИстина();
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Справочник.Номенклатура_Удалить"))
		.ЭтоЛожь();
	Настройки.Справочник.Исключения = ЮТКоллекции.ЗначениеВМассиве("*Удалить");
	УстановитьНастройки(Настройки);
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Справочник.Номенклатура_Удалить"))
		.ЭтоИстина();
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Справочник.Удалить"))
		.ЭтоЛожь();
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Справочник.Удалить_Номенклатура"))
		.ЭтоЛожь();
	Настройки = Новый Структура("Использовать, Исключения", Истина, ЮТКоллекции.ЗначениеВМассиве("Удалить*"));
	УстановитьНастройки(Настройки);
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Документ.Удалить_Номенклатура"))
		.ЭтоИстина();
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Удалить_Справочник.Номенклатура"))
		.ЭтоИстина();
	ЮТест.ОжидаетЧто(ЮТДымовыеТестыСлужебныйКлиентСервер.ИсключитьТест("Справочник.Номенклатура_Удалить"))
		.ЭтоЛожь();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьНастройки(Настройки)
	
	ЮТДымовыеТестыСлужебныйКлиентСервер.Инициализация(Новый Структура("ДымовыеТесты", Настройки));
	
КонецПроцедуры

#КонецОбласти
